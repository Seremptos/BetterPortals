plugins {
    id 'java'
}

allprojects {
    apply plugin: 'java'

    group = "com.lauriethefish.betterportals"

    // Optional, just for showing name
    afterEvaluate {
        println("Evaluating project ${project.group}:${archivesBaseName}:${version}")
    }

    test {
        useJUnitPlatform()
    }

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    compileJava.options.encoding = 'UTF-8'

    dependencies {
        implementation 'com.google.inject:guice:5.1.0'
        implementation 'com.google.inject.extensions:guice-assistedinject:5.1.0'
        implementation 'org.jetbrains:annotations:23.0.0'
        implementation 'com.google.code.findbugs:jsr305:3.0.2'

        compileOnly 'org.projectlombok:lombok:1.18.36'
        annotationProcessor 'org.projectlombok:lombok:1.18.36'
        testCompileOnly 'org.projectlombok:lombok:1.18.36'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'

        // Use JUnit BOM to align versions automatically
        testImplementation platform('org.junit:junit-bom:5.12.0')

        // JUnit 5 dependencies
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

        // Ensure JUnit Platform dependencies are correctly included
        testImplementation 'org.junit.platform:junit-platform-engine'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks {
        compileJava {
            options.release.set(21)
        }
    }
}

jar.enabled = false
build.enabled = false

subprojects {
    if (project.findProperty('BP_DEVELOPER_BUILD') == '1') {
        version = gradle.ext.versionName + '-' + "git rev-parse --verify --short HEAD".execute().text.trim().toLowerCase()
    } else {
        version = gradle.ext.versionName
    }

    archivesBaseName = "${rootProject.name}-${project.name}"
}